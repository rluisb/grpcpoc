buildscript {
    ext {
        springBootVersion = '2.1.4.RELEASE'
        protobufVersion = '0.8.8'
    }
    repositories {
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url "https://repo.spring.io/snapshot" }
        maven { url "https://repo.spring.io/milestone" }
        maven { url 'http://repository.jboss.org/maven2/' }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("com.google.protobuf:protobuf-gradle-plugin:${protobufVersion}")
    }
}

apply plugin: 'java'
apply plugin: 'com.google.protobuf'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'application'

group = 'com.github.rluisb'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'
mainClassName = 'com.github.rluisb.grpc.server.GrpcServerApplication'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
    maven { url "https://plugins.gradle.org/m2/" }
}

ext {
    set('springCloudVersion', 'Greenwich.SR1')
}

dependencies {
    implementation('org.springframework.boot:spring-boot-starter-actuator')
    implementation('org.springframework.cloud:spring-cloud-starter-config')
    implementation('org.apache.commons:commons-lang3:3.8.1')
    implementation('io.grpc:grpc-netty-shaded:1.12.0')
    implementation('io.grpc:grpc-protobuf:1.12.0')
    implementation('io.grpc:grpc-stub:1.12.0')
    implementation("io.grpc:grpc-services:1.12.0")
    implementation('com.github.davidmoten:rxjava2-jdbc:0.2.4')
    runtimeOnly('org.springframework.boot:spring-boot-devtools')
    compileOnly('org.projectlombok:lombok')
    annotationProcessor('org.projectlombok:lombok')

    if (JavaVersion.current().isJava9Compatible()) {
        implementation('javax.annotation:javax.annotation-api:1.3.1')
    }

    protobuf files("proto/")
    testImplementation('org.springframework.boot:spring-boot-starter-test')
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.0.0'
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.0.0-pre2'
        }
    }
    generateProtoTasks {
        ofSourceSet('main')*.plugins {
            grpc {}
        }
    }
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}


